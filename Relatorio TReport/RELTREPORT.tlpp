#INCLUDE 'Protheus.ch'
#INCLUDE 'TOPCONN.ch'

/*/{Protheus.doc} RELTREPORT
Relatorio utilizando o TReport
@type function
@version  1.0
@author Josue Oliveira
@since 10/12/2022
@see https://terminaldeinformacao.com/knowledgebase/getnextalias/
@see https://tdn.totvs.com/display/public/framework/TReport
/*/

User Function RELTREPORT()
    
    Local oReport
    Local cAlias := GetNextAlias() //Funcao para pegar o proximo alias disponivel

    oReport := STRUCTUREBUILDER(cAlias)

    oReport:printDialog()

Return


/*/{Protheus.doc} QUERYBUILDER
Funcao que estrutura a Query SQL
@type function
@version  1.0
@author Josue Oliveira 
@since 10/12/2022
@see https://terminaldeinformacao.com/knowledgebase/beginsql-endsql/
@seer https://tdn.totvs.com/pages/releaseview.action?pageId=24347039
/*/

Static Function QUERYBUILDER(oReport,cAlias)

    Local oSecao1 := oReport:Section(1)

    oSecao1:BeginQuery()

        BEGINSQL Alias cAlias

            SELECT  B.B1_FILIAL AS FILIAL, 
                    B.B1_COD AS CODIGO, 
                    B.B1_DESC AS DESCRICAO
                    FROM %Table:SB1% B 
                    WHERE B.D_E_L_E_T_ = ''

        ENDSQL

    oSecao1:EndQuery()
    oReport:SetMeter((cAlias) -> (RecCount()))

    oSecao1:Print()

Return


/*/{Protheus.doc} STRUCTUREBUILDER
Funcao que cria a estrutura do relaorio
@type function
@version  1.0
@author Josue Oliveira
@since 10/12/2022
@see https://tdn.totvs.com/display/public/framework/TReport
@see https://tdn.totvs.com/display/public/framework/TRSection
/*/
Static Function STRUCTUREBUILDER(cAlias)

    Local cTitle   := "Relatorio de produtos"
    Local cHelp     := "Permite imprimir relatório de produtos"
    Local oReport   
    Local oSection1

    //Instanciando a classe TReport
    oReport := TReport():New('RELTREPORT',cTitle,/**/,{|oReport| QUERYBUILDER(oReport,cAlias)},cHelp)
    
    //Secao
    oSection1 := TRSection():New(oReport, "Produtos", {"SB1"})
    TRCell():New(oSection1,"FILIAL",   "SB1","Filial")
    TRCell():New(oSection1,"CODIGO",   "SB1","Codigo")
    TRCell():New(oSection1,"DESCRICAO","SB1","Descricao")


return oReport
